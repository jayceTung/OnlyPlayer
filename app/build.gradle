apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    signingConfigs {
        debug {

        }
        release {
            keyAlias 'onlyplayer'
            storeFile file('../key/onlyplayer.jks')
            if (System.console() != null) {
                keyPassword System.console().readLine("\nKey password: ")
                storePassword System.console().readLine("\nKeystore password: ")
            }
            keyPassword '123456'
            storePassword '123456'
        }
    }

    defaultConfig {
        applicationId "com.asuper.onlyplayer"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "0.1"

        //dex突破65535方法数
        multiDexEnabled true

        manifestPlaceholders = [app_label: "@string/app_name"]
    }

    packagingOptions{
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
            manifestPlaceholders = [app_label: "@string/app_name_debug"]
        }
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'


            android.applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    // 删除unaligned apk
                    if (output.zipAlign != null) {
                        output.zipAlign.doLast {
                            output.zipAlign.inputFile.delete()
                        }
                    }
                }
            }

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为boohee_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "${rootDir}/apk/OnlyPlayer_v${defaultConfig.versionName}_${buildTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(fileName)
                    }
                }
            }
        }
    }

    productFlavors {
        _360 {
            manifestPlaceholders = [CHANNEL_NAME: "_360"]
        }
        meizu {
            manifestPlaceholders = [CHANNEL_NAME: "MEIZU"]
        }

    }
}


def buildTime() {
    return new Date().format("yyyy-MM-dd")
}

def hostName() {
    return System.getProperty("user.name") + "@" + InetAddress.localHost.hostName
}

def revision() {
    def code = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = code
    }
    return code.toString()
}

def gitVersion() {
    if (!System.getenv('CI_BUILD')) { // 不通过CI进行build的时候返回01
        // don't care
        return 1
    }
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile 'junit:junit:4.12'

    compile([
            'com.android.support:appcompat-v7:23.1.0',
            'tv.danmaku.ijk.media:ijkplayer-java:0.6.2',
            'tv.danmaku.ijk.media:ijkplayer-armv7a:0.6.2',
            'tv.danmaku.ijk.media:ijkplayer-exo:0.6.2'
    ])

    compile 'com.android.support:preference-v7:23.0.1'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.github.clans:fab:1.6.4'
    compile 'com.jakewharton:butterknife:7.0.1'
    // retrofit2 + okhttp3
    compile 'com.squareup.retrofit2:retrofit:2.0.+'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.+'
    compile 'com.squareup.retrofit2:converter-gson:2.0.+'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    //Rxjava + Rxandroid
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'
    //umeng
    compile 'com.umeng.analytics:analytics:latest.integration'
    //multidex
    compile 'com.android.support:multidex:1.0.0'
}
